BalloonIsOpen
{
    "balloon" _in_ status
}

displayjournal
{
	if journalopen; "\1\s[10]"
    else; "\1\s[-1]"
}

OnTranslate
{
	_talk = reference0
	
	if reference1 == "" && reference2 == "" 
	{
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	TOSTR(_talk)
}

OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

OnJournalOpen
{
	if journalopen == 0
	{
		journalopen = 1
		"\1\s[10]"
	}
	else
	{
		journalopen = 0
		"\1\s[-1]"
	}
}

OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
	elseif reference0 == "s"; OnJournalOpen
}

OnNotifyDressupInfo
{
	CurrentDressups = IARRAY
	foreach reference; _page
	{
		_page = SPLIT(_page,C_BYTE1)
		_id = _page[0]
		_category = _page[1]
		_item_name = _page[2]
		
		LOGGING("_page[4]: %(_page[4])")
		if _id == "1" && _page[1] == "Page" && _page[4] == "1"
		{
			CurrentPage = _item_name
			LOGGING("====================================================================")
			LOGGING("CurrentPage: %(CurrentPage)")
		}
	}
}

OnSecondChange
{
    if GETSECCOUNT >= FishingTime
    {
         SetFishingTime
         "\0\s[20]Fishing Time!"
    }
}

OnSurfaceRestore
{
	displayjournal
	--
	"\0\s[0]"
}

OnTogglePage
{
	_index = ASEARCH(TOSTR(CurrentPage),PageArray)
    _index++
    if _index >= ARRAYSIZE(PageArray); _index = 0
    "\1\![bind,Page,%(PageArray[_index]),1]"
}

OnTogglePageReverse
{
    _index = ASEARCH(TOSTR(CurrentPage),PageArray)
    _index--
    if _index < 0; _index = ARRAYSIZE(PageArray)
    "\1\![bind,Page,%(PageArray[_index]),1]"
}

OnWindowStateRestore
{
	displayjournal
	--
	"\0\s[0]"
}

PageArray : array
{
	"Open"
	"Gardening"
	"Lost Items"
}

WearingDressup
{
	if ASEARCH(_argv[0],CurrentDressups) != -1; 1
	else; 0
}