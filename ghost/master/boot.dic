OnFirstBoot
{
	variable.initialize
	--
	EntryFlags = IARRAY
	--
	LastTalk = ""
	--
	"\![raise,OnBoot]"
}

OnInitialize
{
	if !ISVAR("fishrate"); fishrate = "Sometimes"
	if reference0 != "reload"
	{
		LastTalk = ""
	}
}

OnBoot
{
	SetFishingTime
	--
	displayjournal
	--
	"\0\s[0]Another day, another fish. "
	"Oh, I forgot my coffee this morning. "
	"I don't know if this is considered a break, but it's nice to do something different for a while. "

}

OnClose
{
	"There’s always next time... "
	"It’ll be a while before the next catch... "
	"Yeah, I think it's time to go home. "
	--
	"\w8\w8\-"
}

variable.initialize
{
	journalopen = 0
	fishrate = "Sometimes"
}

SetFishingTime
{
    if fishrate == "Often"; FishingTime = GETSECCOUNT + (RAND(120) + 60) //1-3 minutes
	if fishrate == "Sometimes"; FishingTime = GETSECCOUNT + (RAND(120) + 180) //3-5 minutes
	if fishrate == "Periodic"; FishingTime = GETSECCOUNT + (RAND(300) + 300) //5-10 minutes
	if fishrate == "Rare"; FishingTime = GETSECCOUNT + (RAND(1200) + 600) //10-30 minutes
	if fishrate == "Random"; FishingTime = GETSECCOUNT + (RAND(1740) + 60) //1-30 minutes
}